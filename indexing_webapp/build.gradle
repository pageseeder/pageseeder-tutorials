/*
 * Assemble and deploy the Web application. 
 */
buildscript {
  repositories {
    jcenter()
    maven { url 'http://dl.bintray.com/robfletcher/gradle-plugins' }
  }
  dependencies {
    classpath (
      'org.akhikhl.gretty:gretty:1.2.4',
      'org.hidetake:gradle-ssh-plugin:1.1.4'
    )
  }
}

// War + Gretty
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'org.hidetake.ssh'

// Dependencies of the project
repositories {
  maven { url 'http://dl.bintray.com/pageseeder/maven' }
  jcenter()
}

// Dependencies for your production and test code
dependencies {

  // Deployed on Berlioz
  runtime (
    'org.slf4j:slf4j-api:1.7.12',
    'org.pageseeder.berlioz:pso-berlioz:0.11.1',
    'org.pageseeder.berlioz:pso-berlioz-kickstart:0.11.1',
    'org.pageseeder.bastille:pso-bastille:0.9.1',
    'org.pageseeder.flint:pso-flint-berlioz:5.1.3.18',
    'org.pageseeder.flint:pso-flint-lucene:5.1.3.18',
    'org.pageseeder.flint:pso-flint-solr:5.1.3.18',
    'commons-io:commons-io:2.4',
    'net.sf.saxon:Saxon-HE:9.6.0-7',
    'ch.qos.logback:logback-core:1.0.11',
    'ch.qos.logback:logback-classic:1.0.11',
    'org.pageseeder.cobble:pso-cobble:0.3.1'
  )

}

// Property for local Jetty dev server
gretty {
  jvmArgs = ["-Dberlioz.mode=${localMode}"]
  host = 'sample.localhost'
  httpPort = 8099
  httpsEnabled = true
  httpsPort = 8444
  contextPath = '/'
  servletContainer = 'jetty9'
}




task deployLocalStatic << {
  copy {
    from webAppDirName
    into localWebapp
    exclude "${webAppDirName}/WEB-INF/config/*-*.xml"
  }
  copy {
    from "${webAppDirName}/WEB-INF/config"
    into "${localWebapp}/WEB-INF/config"
    include "*-${localMode}.xml"
  }
}

task deployLocalClasses(type: Copy, group: 'allette') {
  description = 'Copy the classes with the local WebApp'
  from compileJava
  from processResources
  into "${localWebapp}/WEB-INF/classes"
}

task deployLocalLibs(type: Sync, group: 'allette') {
  description = 'Sync the libraries with the local WebApp'
  from configurations.runtime  - configurations.providedCompile
  into "${localWebapp}/WEB-INF/lib"
}

task deployLocal (group: 'allette') {
  description = 'Deploys the WebApp locally'
  dependsOn deployLocalStatic
  dependsOn deployLocalClasses
  dependsOn deployLocalLibs
}

